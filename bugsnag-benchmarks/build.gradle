plugins {
    id "com.android.library"
    id "androidx.benchmark"
    id "kotlin-android"
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.benchmark.junit4.AndroidBenchmarkRunner"

        // suppress warnings that the CPU clock is not locked, and allow running on an emulator.
        // it's not possible to lock the CPU clock without rooting a device, which isn't possible
        // on our CI setup currently.
        testInstrumentationRunnerArgument "androidx.benchmark.suppressErrors", "EMULATOR,LOW_BATTERY,UNLOCKED"

        // must be one of None, Sampled, Method, ConnectedSampled, ConnectedAllocation
        // see https://developer.android.com/studio/profile/benchmark#profiling
        testInstrumentationRunnerArgument 'androidx.benchmark.profiling.mode', 'Sampled'

        // TODO this enables output on the target device, but the version of the library doesn't
        // seem to copy things across. updating to alpha-11 may help but we need to resolve
        // a clash with the artefact's AAR metadata first.
        testInstrumentationRunnerArgument 'androidx.benchmark.output.enable', 'true'

        // To grab trace files from the device, call:
        // adb shell ls /storage/emulated/0/Download
        // adb pull /storage/emulated/0/Download/<my-file>.trace
    }

    testBuildType = "release"
    buildTypes {
        debug {
            // Since debuggable can"t be modified by gradle for library modules,
            // it must be done in a manifest - see src/androidTest/AndroidManifest.xml
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "benchmark-proguard-rules.pro"
        }
        release {
            isDefault = true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.72"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.benchmark:benchmark-junit4:1.1.0-alpha01"

    // Add your dependencies here. Note that you cannot benchmark code
    // in an app module this way - you will need to move any code you
    // want to benchmark to a library module:
    // https://developer.android.com/studio/projects/android-library#Convert
    implementation(project(":bugsnag-android"))
}
